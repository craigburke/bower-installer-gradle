plugins {
    id 'com.jfrog.bintray' version '1.2'
    id 'com.gradle.plugin-publish' version '0.9.1'
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

targetCompatibility = 1.6

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

group = 'com.craigburke.gradle'
version = '1.3.1'

ext {
    artifactId = 'bower-installer'
    description = 'Bower Installer Gradle Plugin'
    license = [
            name: 'Mozilla Public License, Version 2.0',
            url : 'https://www.mozilla.org/MPL/2.0/'
    ]
}

configurations {
    provided
    testCompile { extendsFrom provided }
}

dependencies {
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.3.10'
    provided 'com.moowork.gradle:gradle-node-plugin:0.10'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}


sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : ''
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : ''
    publications = ['maven']

    pkg {
        repo = 'gradle'
        userOrg = 'craigburke'
        name = project.artifactId
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId project.artifactId
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.artifactId
                    description project.description
                    url 'https://github.com/craigburke/bower-installer-gradle'
                    scm {
                        url 'https://github.com/craigburke/bower-installer-gradle'
                        connection 'scm:https://github.com/craigburke/bower-installer-gradle'
                        developerConnection 'scm:https://github.com/craigburke/bower-installer-gradle'
                    }
                    licenses {
                        license {
                            name project.license.name
                            url project.license.url
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'craigburke'
                            name 'Craig Burke'
                            email 'craig@craigburke.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar, javadoc

pluginBundle {
    website = 'https://github.com/craigburke/bower-installer-gradle'
    vcsUrl = 'https://github.com/craigburke/bower-installer-gradle'
    description = 'Uses bower-installer to manage client-side dependencies alongside the other dependencies in build.gradle.'
    tags = ['bower', 'bower-installer']

    plugins {
        bowerInstaller {
            id = 'com.craigburke.bower-installer'
            displayName = 'Bower Installer plugin'
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}